ext {
    aGroup = 'ru.primetalk'
// To publish to maven central:
// 1. change version — remove SNAPSHOT.
// 2. put _${scalaMajorVersion} in artifact names. (already done)
// 3. enableSigning = true
// 4. issue at shell:
//     ./gradlew uploadArchives --no-daemon
// 5. Login to oss sonatype https://oss.sonatype.org/index.html
// 6. Do migration:
//    - remove synapse-grid-examples from repository,
//    - Close repository,
//    - Release repository.
// The artifacts should appear in Maven Central.
//

//    aVersion = '1.4.3-SNAPSHOT'
//    enableSigning = false
    aVersion = '1.4.5'//'1.2.0-SNAPSHOT'

    enableSigning = !aVersion.contains('SNAPSHOT')


    scalaMajorVersion = '2.11'
    scalaFullVersion = "${scalaMajorVersion}.7"
//    scalaMajorVersion = '2.10'
//    scalaFullVersion = "${scalaMajorVersion}.5"
    scalaTest = "org.scalatest:scalatest_${scalaMajorVersion}:2.2.5"
    shapelessDep = "com.chuusai:shapeless_${scalaMajorVersion}:2.2.5"

    scalaLib = "org.scala-lang:scala-library:${scalaFullVersion}"
    scalaReflect = "org.scala-lang:scala-reflect:${scalaFullVersion}"

    akkaVersion = '2.3.12'
    slf4j = 'org.slf4j:slf4j-api:1.7.12'
    logback = 'ch.qos.logback:logback-classic:1.1.3'
    rxDep = "io.reactivex:rxscala_${scalaMajorVersion}:0.25.0"
    junit = 'junit:junit:4.12'
    pegdown = 'org.pegdown:pegdown:1.5.0'
    specs2 = "org.specs2:specs2-core_${scalaMajorVersion}:3.6.4"
    specs2Junit = "org.specs2:specs2-junit_${scalaMajorVersion}:3.6.4"

    akka = [
            "com.typesafe.akka:akka-actor_$scalaMajorVersion:$akkaVersion",
            "com.typesafe.akka:akka-remote_$scalaMajorVersion:$akkaVersion",
            "com.typesafe.akka:akka-slf4j_$scalaMajorVersion:$akkaVersion"
    ]
}
// Troubleshooting:
// gradle OutOfMemoryError: PermGen space:
// JAVAOPTS="-Xmx1024M -XX:MaxPermSize=512M"
task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

gradle.taskGraph.whenReady { taskGraph ->
    if (enableSigning && taskGraph.allTasks.any { it instanceof Sign }) {
        // Use Java 6's console to read from the console (no good for a CI environment)
        Console console = System.console()
        console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"

        def id = "855C7687"//console.readLine("PGP Key Id: ")
        def file = "/home/zhizhelev/.gnupg/secring.gpg"//console.readLine("PGP Secret Key Ring File (absolute path): ")
        def password = console.readPassword("PGP Private Key Password: ")

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }

        console.printf "\nThanks.\n\n"
    }
}



buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.maiflai:gradle-scalatest:0.8"
    }
}

allprojects {
    apply plugin: 'idea'

    repositories {
//        mavenLocal()
        mavenCentral()
        maven{ url="http://dl.bintray.com/scalaz/releases" }
    }
}


subprojects {
    apply plugin: 'scala'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'eclipse'
    apply plugin: "com.github.maiflai.scalatest"

    group = aGroup
    version = aVersion


    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    dependencies {
        compile scalaLib

        testCompile junit
        testRuntime pegdown // it is required by scalatest gradle plugin.
        testCompile scalaTest
        testCompile specs2
        testCompile specs2Junit
//                {
//            exclude group: "org.scalaz.stream" // » scalaz-stream_2.11
//        }
    }

    // making source.jar
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    // making javadoc.jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // adding source.jar and javadoc.jar to artifacts
    artifacts {
        archives jar

        archives sourcesJar
        archives javadocJar
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.useAnt = false
        scalaCompileOptions.useCompileDaemon = true
        // optionally specify host and port of the daemon:
        scalaCompileOptions.daemonServer = "localhost:4243"

        scalaCompileOptions.additionalParameters = ["-feature"]
    }


    if(new File(projectDir,"src/test/scala").exists()) {
        /**
         * Run Spec2 tests
         */
        task specs(type: JavaExec, dependsOn: testClasses) {
            main = 'org.specs2.runner.files'
            args = ['console junitxml html']
            classpath sourceSets.main.runtimeClasspath
            classpath sourceSets.test.runtimeClasspath
            classpath configurations.testRuntime
            classpath configurations.runtime
        }
        test.dependsOn specs
    }
/*  publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven{ // declare properties in ~/.gradle/gradle.properties
                url nexusRep
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
            }
        }
    }
*/

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-http:2.9"
    }

    if (enableSigning) {
        apply plugin: 'maven-publish'
        uploadArchives {
            repositories {
                mavenDeployer {
                    configuration = configurations.deployerJars
//                "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//                "https://oss.sonatype.org/content/repositories/snapshots"
                    repository(url: repositoryUrl) {
                        authentication(userName: repositoryUsername, password: repositoryPassword)
                    }
//              repository (url: nexusRep) {
//					authentication(userName: nexusUsername, password: nexusPassword)	// declare credentials in ~/.gradle/gradle.properties
//				}
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    pom.project {
                        artifactId "${project.name}_${scalaMajorVersion}"
                        packaging 'jar'
                        description 'SynapseGrid is a framework for constructing reactive real-time immutable data flow systems. ' +
                                '-core contains everything to run a single-threaded system, ' +
                                '-akka contains everything to run systems over Akka actors, ' +
                                '-slf4j - enables logging, ' +
                                '-concurrent - running systems directly over ExecutorContext without the need for Akka, ' +
                                '-examples - a few test systems. Also there are ' +
                                '' +
                                "The current version is $aVersion"
                        url 'https://github.com/Primetalk/SynapseGrid'
                        parent {
                            groupId 'org.sonatype.oss'
                            artifactId 'oss-parent'
                            version 7
                        }
                        licenses {
                            license {
                                name 'BSD Software License, 2-clause version'
                                url 'https://github.com/Primetalk/SynapseGrid/blob/master/LICENSE.md'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'zhizhelev'
                                name 'Arseniy Zhizhelev'
                                email 'zhizhelev@primetalk.ru'
                            }
                            developer {
                                id 'nehaev'
                                name 'Anton Nehaev'
                                email 'nehaev@primetalk.ru'
                            }
                            developer {
                                id 'popov'
                                name 'Pavel Popov'
                                email 'popov@primetalk.ru'
                            }

                        }

                        scm {
                            url 'https://github.com/Primetalk/SynapseGrid'
                            connection 'scm:git:git@github.com:Primetalk/SynapseGrid.git'
                            developerConnection 'scm:git:git@github.com:Primetalk/SynapseGrid.git'
                        }

                    }
                }
            }
        }
        signing {
            required { enableSigning && gradle.taskGraph.hasTask("uploadArchives") }
            sign configurations.archives
        }
    }
}
project(':synapse-grid-core') {}

project(':synapse-grid-slf4j') {
    dependencies {
        compile project(':synapse-grid-core')
        compile slf4j
        testRuntime logback
    }
}

project(':synapse-grid-akka') {
    dependencies {
        compile project(':synapse-grid-slf4j')
        compile akka

        testRuntime logback
    }
}

project(':synapse-grid-shapeless') {
    dependencies {
        compile project(':synapse-grid-core')
        compile shapelessDep
    }
}

project(':synapse-grid-concurrent') {
    dependencies {
        compile project(':synapse-grid-core')
        compile shapelessDep
    }
}

project(':synapse-grid-rx') {
    dependencies {
        compile project(':synapse-grid-core')
        compile rxDep
    }
}

project(':synapse-grid-examples') {
    uploadArchives.enabled = false
    dependencies {
        compile project(':synapse-grid-akka')
        compile project(':synapse-grid-rx')
        testRuntime logback
    }
}

